: b.Type builtin.Type builtin.Type.eff ;
: Type builtin.Type ; !( -- b.Type )
: eff.drop builtin.eff.drop ; !( Type -- )
: eff.push builtin.eff.push ; !( Type -- )
: eff.dup builtin.eff.dup ; !( Type -- )
: newtype builtin.newtype Type eff.push ; immediate
: paramtype builtin.paramtype Type eff.push ; immediate
: a paramtype ;
: b paramtype ;
: c paramtype ;

: Quot builtin.Quot ; !( -- Type )
: Int builtin.Int ; !( -- Type )
: Pointer newtype ;

: cell 8 ;

: drop.eff a eff.drop ;
: dup.eff a eff.dup ;
: drop trait ; eff.attach drop.eff
: dup trait ; eff.attach dup.eff

: grow [ 0x48 0x83 0xeb 0x08 ] X ; !( -- Int )
: Int.drop [ 0x48 0x83 0xc3 0x08 ] X ; !( Int ) impl> drop
: Pointer.drop Int.drop ; !( Pointer ) impl> drop
: Type.drop [ 0x48 0x83 0xc3 0x08 ] X ; !( Type ) impl> drop
: >rax [ 0x48 0x8b 0x03 ] X Int.drop ;
: >rcx [ 0x48 0x8b 0x0b ] X Int.drop ;
: >rdx [ 0x48 0x8b 0x13 ] X Int.drop ;
: >rdi [ 0x48 0x8b 0x3b ] X Int.drop ;
: >rsi [ 0x48 0x8b 0x33 ] X Int.drop ;
: >r8 [ 0x4c 0x8b 0x03 ] X Int.drop ;
: >r9 [ 0x4c 0x8b 0x0b ] X Int.drop ;
: rax> grow [ 0x48 0x89 0x03 ] X ;
: rcx> grow [ 0x48 0x89 0x0b ] X ;
: Int.dup >rax grow rax> ; impl> dup
: Type.dup Int.dup ; !( Type -- Type Type ) impl> dup
: Pointer.dup Int.dup ; !( Pointer -- Pointer Pointer ) impl> dup
: Quot.dup Int.dup ; !( Quot -- Quot Quot ) impl> dup
: Int.swap >rax >rcx rax> rcx> ;
: Pointer.swap Int.swap ; !( Pointer Pointer -- Pointer Pointer )
: Pointer.Int.swap Int.swap ; !( Pointer Int -- Int Pointer )
: Type.swap Int.swap ; !( Type Type -- Type Type )
: Quot.swap Int.swap ; !( Quot Quot -- Quot Quot )
: Quot.Type.swap Int.swap ; !( Quot Type -- Type Quot )

: lent.eff a dup eff.drop eff.push b eff.push ;
: lent trait ; eff.attach lent.eff

: + >rcx >rax [ 0x48 0x01 0xc8 ] X rax> ;
: - >rcx >rax [ 0x48 0x29 0xc8 ] X rax> ;
: p+ + ; !( Pointer Int -- Pointer )
: p- - ; !( Pointer Int -- Pointer )
: pp+ + ; !( Pointer Pointer -- Pointer )
: pp- - ; !( Pointer Pointer -- Pointer )
: eq
  >rcx >rax
  [ 0x48 0x39 0xc8
    0x0f 0x94 0xc0
    0x48 0x0f 0xb6 0xc0 ] X
  rax>
;
: <
  >rcx >rax
  [ 0x48 0x39 0xc8
    0x0f 0x9c 0xc0
    0x48 0x0f 0xb6 0xc0 ] X
  rax>
;

: >rbx [ 0x48 0x8b 0x1b ] X ; !( Int -- )
: rbx> grow [ 0x48 0x89 0x1b ] X ;
: sp@ rbx> ; !( -- Pointer )
: sp! >rbx ; !( Pointer -- )

: band >rcx >rax [ 0x48 0x21 0xc8 ] X rax> ;
: shr >rcx >rax [ 0x48 0xd3 0xe8 ] X rax> ;
: @ >rax [ 0x48 0x8b 0x00 ] X rax> ; !( Pointer -- Int )
: u32@ >rax [ 0x8b 0x00 ] X rax> ; !( Pointer -- Int )
: ! >rax >rcx [ 0x48 0x89 0x08 ] X ; !( Int Pointer -- )
: p@ >rax [ 0x48 0x8b 0x00 ] X rax> ; !( Pointer -- Pointer )
: p! >rax >rcx [ 0x48 0x89 0x08 ] X ; !( Pointer Pointer -- )
: c! >rax >rcx [ 0x88 0x08 ] X ; !( Int Pointer -- )
: Int.pick3.eff
  b dup eff.drop
  a dup eff.drop
  Int eff.drop
  Int eff.push eff.push eff.push Int eff.push
;
: Int.pick3 sp@ cell cell + cell + p+ @ ; eff.attach Int.pick3.eff
: +! dup @ Int.pick3 + Pointer.Int.swap ! Int.drop ;

: dp builtin.dp ; !( -- Pointer )
: dp@ dp p@ ;
: cp builtin.cp ; !( -- Pointer )
: here dp@ ;
: allot dp +! ;
: , here ! cell allot ;

rem syscalls
: write-syscall 0x0f op 0x05 op ; immediate
: syscall0 >rax write-syscall >rax ;
: syscall1 >rax >rdi write-syscall rax> ;
: syscall2 >rax >rsi >rdi write-syscall rax> ;
: syscall3 >rax >rdx >rsi >rdi write-syscall rax> ;
: exit 60 syscall1 Int.drop ;

rem dataflow
: spill.unsafe 0xff op 0x33 op 0xff op 0x33 op ;
: drop.unsafe 0x48 op 0x83 op 0xc3 op 0x08 op ;
: restore.unsafe 0x8f op 0x03 op 0x8f op 0x03 op ;
: grow.unsafe 0x48 op 0x83 op 0xeb op 0x08 op ;
: >rax.unsafe >rax ; !( )
: >rcx.unsafe >rcx ; !( )
: rax>.unsafe rax> ; !( )
: rcx>.unsafe rcx> ; !( )
: swap.unsafe [ >rcx.unsafe >rax.unsafe rcx>.unsafe rax>.unsafe ] postcompile ;
: dip
  a dup eff.drop
  spill.unsafe drop.unsafe
  Quot.Type.swap compile
  grow.unsafe restore.unsafe
  eff.push
; immediate
: 2dip
  a dup eff.drop
  b dup eff.drop
  spill.unsafe drop.unsafe
  spill.unsafe drop.unsafe
  [ Quot.Type.swap compile ] dip
  grow.unsafe restore.unsafe
  grow.unsafe restore.unsafe
  eff.push
  eff.push
; immediate
: 3dip
  a dup eff.drop
  b dup eff.drop
  c dup eff.drop
  spill.unsafe drop.unsafe
  spill.unsafe drop.unsafe
  spill.unsafe drop.unsafe
  [ Quot.Type.swap compile ] 2dip
  grow.unsafe restore.unsafe
  grow.unsafe restore.unsafe
  grow.unsafe restore.unsafe
  eff.push
  eff.push
  eff.push
; immediate
: keep
  a dup eff.drop dup eff.push
  spill.unsafe
  Quot.Type.swap compile
  grow.unsafe restore.unsafe
  eff.push
; immediate
: 2keep
  a dup eff.drop b dup eff.drop dup eff.push [ dup eff.push ] dip
  spill.unsafe
  swap.unsafe
  spill.unsafe
  swap.unsafe
  [ compile ] 2dip
  grow.unsafe restore.unsafe
  grow.unsafe restore.unsafe
  eff.push
  eff.push
; immediate
: bi
  Quot.swap
  a dup eff.drop
  dup eff.push
  spill.unsafe
  Quot.Type.swap compile
  dup eff.push
  grow.unsafe restore.unsafe
  Quot.Type.swap compile
  drop
; immediate
: bi@
  Quot.dup
  swap.unsafe
  spill.unsafe drop.unsafe
  compile
  grow.unsafe restore.unsafe
  compile
; immediate

rem

: to-int ; !( Pointer -- Int )

rem op

: take-byte shr 0xff band ;
: fixup-byte take-byte Pointer.Int.swap c! ;
: next-byte [ 1 p+ ] dip ;
: fixup-lendian32
  [ 0 fixup-byte ] 2keep next-byte
  [ 8 fixup-byte ] 2keep next-byte
  [ 16 fixup-byte ] 2keep next-byte
  [ 24 fixup-byte ] 2keep next-byte
  drop drop
;
: op-lendian32
  dup 0 take-byte op
  dup 8 take-byte op
  dup 16 take-byte op
  24 take-byte op
;

: get-rel cp @ Int.swap - ;
: fixup-to-end 4 p+ to-int ;
: fixup-by-here dup fixup-to-end get-rel fixup-lendian32 ;
: write-to-here to-int cp p@ fixup-to-end - op-lendian32 ;

rem controlflow

: write-je 0x0f op 0x84 op cp p@ 0 op-lendian32 ;
: write-when
  0x48 op 0x8b op 0x03 op
  0x48 op 0x83 op 0xc3 op 0x08 op
  0x48 op 0x83 op 0xf8 op 0x00 op
  write-je
;
: when
  Int eff.drop
  write-when [ compile ] dip fixup-by-here
; immediate

: write-while
  0x48 op 0x8b op 0x03 op
  0x48 op 0x83 op 0xc3 op 0x08 op
  0x48 op 0x83 op 0xf8 op 0x00 op
  write-je
;
: while
  Int eff.drop
  Int eff.push
  cp p@
  [ Quot.swap compile ] dip
  write-while
  Int eff.drop
  [ compile ] 2dip 0xe9 op
  Pointer.swap write-to-here
  fixup-by-here
; immediate
